/**
 * gradle clean build
 */
plugins {
    id 'java'
}

repositories {
    mavenCentral()
}
 
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
}

sourceCompatibility = '1.9'
targetCompatibility = '1.9'

def projectName = 'jeval'
def releaseFileName = "${projectName}.zip"
def releaseDir = "release/$projectName/$projectName"

jar { 
    manifest { 
        attributes 'Implementation-Version': "${rootProject.version}",
                   'Main-Class': 'id.jeval.Main', 
                   'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
    }
}

task packageDistribution(type: Zip) {
    archiveFileName = "$releaseFileName"
    destinationDirectory = file("$projectDir/release")
    from file("$projectDir/release/$projectName")
}

packageDistribution.doLast {
    delete "$projectDir/release/$projectName"
}

import org.apache.tools.ant.filters.ReplaceTokens
task updateVersion(type:Copy) {
    from "${project.rootDir}/README.md"
    into releaseDir
    filter(ReplaceTokens, tokens: [version: rootProject.version])
}

task createRelease(type: Copy, dependsOn: updateVersion) {
    from jar.archiveFile
    from file("$projectDir/libs")
    from "${project.rootDir}/LICENSE-2.0.txt"
    from file("$projectDir/src/jeval")
    from file("$projectDir/src/jeval.bat")
    into releaseDir
}

task cleanRelease(type: Delete) {
    delete "$projectDir/release"
}

packageDistribution.dependsOn createRelease
assemble.dependsOn packageDistribution
clean.dependsOn cleanRelease

task copyReadme(type: Copy) {
    from("$projectDir/README.md")
    into("$projectDir/src/main/resources")
}

compileJava.dependsOn copyReadme
