plugins {
    id 'com.diffplug.spotless'
}

apply plugin : 'eclipse'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "io.github.lambdaprime:id.xfunction:19.0"

    // transitively export following dependency to tests project
    apiElements fileTree(include: ['*.jar'], dir: 'libs')
    apiElements "io.github.lambdaprime:id.xfunction:19.0"
}

configurations {
  customConfig.extendsFrom implementation
}

jar { 
    manifest { 
        attributes 'Implementation-Version': "${rootProject.version}",
                   'Main-Class': 'id.jeval.Main'
    }
}

def packageDir = "$buildDir/package/${projectName}"

ext.artifacts = fileTree(include: ['*'], dir: "$packageDir/libs")

task createRelease(type: Zip) {
    archiveFileName = "$releaseFileName"
    destinationDirectory = file("$projectDir/release")
    from file("$packageDir/..")
}

task copyLibs(type: Copy) {
    from configurations.customConfig
    from jar.archiveFile
    into "${packageDir}/libs"
}

import org.apache.tools.ant.filters.ReplaceTokens
task updateVersion(type:Copy) {
    from "${project.rootDir}/README.md"
    into packageDir
    filter(ReplaceTokens, tokens: [version: rootProject.version])
}

task copyReadme(type: Copy) {
    from("../README.md")
    into("$buildDir/resources/main/jeval")
}

task createPackage(type: Copy, dependsOn: copyLibs) {
    from "${project.rootDir}/README.md"
    from "${project.rootDir}/LICENSE-2.0.txt"
    from file("$projectDir/src/jeval")
    from file("$projectDir/src/jeval.bat")
    into packageDir
}

task cleanRelease(type: Delete) {
    delete "$packageDir"
    delete "$projectDir/release"
}

compileJava.dependsOn copyReadme
createRelease.dependsOn createPackage
assemble.dependsOn createRelease
clean.dependsOn cleanRelease
processResources.dependsOn copyReadme

// run spotless and format code before the build
classes.dependsOn spotlessApply
